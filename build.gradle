plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '6.11.0'
    id 'org.jreleaser' version '1.18.0'
}

group = 'com.layer1.clients'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

spotless {
    java {
        googleJavaFormat()
    }
}

def stagingDirectory = layout.buildDirectory.dir('staging-deploy')

jreleaser {
//    project {
//        name = 'layer1-sdk-java-auth'
//        description = 'Layer1 Java SDK for Authentication'
//        website = 'https://github.com/bvnk/layer1-sdk-java-auth'
//        authors = ['BVNK <layer1@bvnk.com>']
//        license = "MIT"
//    }
//
//    release {
//        github {
//            name = 'layer1-sdk-java-auth'
//            draft = false
//        }
//    }

    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository(stagingDirectory.get().asFile.absolutePath)
                }
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "${group}"
            artifactId = "${project.name}"

            from components.java

            pom {
                name ="${project.name}"
                description = 'Layer1 Java SDK for Authentication'
                url = 'https://github.com/bvnk/layer1-sdk-java-auth'
                inceptionYear = '2025'
                licenses {
                    license {
                        name = 'MIT'
                    }
                }
                developers {
                    developer {
                        id = 'bvnk'
                        name = 'BVNK'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/bvnk/layer1-sdk-java-auth.git'
                    developerConnection = 'scm:git:ssh://github.com/bvnk/layer1-sdk-java-auth.git'
                    url = 'https://github.com/bvnk/layer1-sdk-java-auth'
                }
            }
        }
    }

    repositories {
        maven {
            url = stagingDirectory
        }
    }
}
